10 REM SOLVE LINEAR PROGRAM VIA SIMPLEX ALGORITHM
15 OPTION BASE 1
20 REM N1 = 0 ' NUMBER OF DECISION VARIABLES
21 REM N2 = 0 ' NUMBER OF CONSTRAINTS (AND SLACK VARIABLES)
30 REM X(N2, N1+N2) ' CENTRAL MATRIX
31 REM B(N2)        ' RIGHT-HAND SIDE VALUES
32 REM C1$(N2)      ' NAMES OF BASIC VARIABLES
33 REM C$(N1+N2)    ' NAMES OF DECISION AND SLACK VARIABLES
34 REM C(N1+N2)     ' COEFFICIENTS FOR OBJECTIVE FUNCTION
35 REM Z(N1+N2)     ' EVAL STEP FOR PIVOT COLUMN SELECTION
36 REM B1(N2)       ' EVAL STEP FOR PIVOT ROW SELECTION
37 REM R()          ' OBJECTIVE COEFFICIENT FOR BASIC VARIABLE
38 REM I0, J0       ' ROW AND COLUMN OF PIVOT CELL
100 REM READ DIMENSIONS
101 READ N1, N2
110 REM BUILD COEFFICIENTS FOR EXPANDED OBJECTIVE FUNCTION
111 DIM C(N1)
112 ARR READ C
113 DIM C(N1 + N2)     ' EXPAND TO INCLUDE SLACK VARIABLES
114 C(J) = 0 FOR J = N1 + 1 TO N1 + N2
120 REM BUILD NAMES OF DECISION VARIABLES AND SLACK VARIABLES
121 DIM C$(N1 + N2)
122 FOR I = 1 TO N1
123 C$(I) = "X" + STR$(I)
124 NEXT I
125 FOR I = 1 TO N2
126 C$(I + N1) = "S" + STR$(I)
127 NEXT I
128 FOR I = 1 TO N2
129 C1$(I) = C$(I + N1)
130 NEXT I
140 REM BUILD CENTRAL MATRIX (CONSTRAINTS)
141 DIM X(N2, N1)
142 MAT READ X
143 DIM X(N2, N1 + N2)     ' EXPAND TO INCLUDE SLACK VARIABLES
144 X(I, J) = 0 FOR J = N1 + 1 TO N1 + N2 FOR I = 1 TO N2
145 X(I, I + N1) = 1 FOR I = 1 TO N2     ' SET EXPANSION TO IDEN MATRIX
160 REM BUILD RIGHT-HAND SIDE VALUES (CONSTANTS)
161 DIM B(N2)
162 ARR READ B
170 REM COPY COEFFICIENTS FOR BASIC VARIABLES
171 DIM R(N2)
172 R(I) = 0 FOR I = 1 TO N2
200 REM PRINT THE TABLEAU
201 PRINT
202 PRINT "TABLEAU"
203 PRINT
204 PRINT " ", "C->",
205 ARR PRINT C,
210 PRINT "CB", "BV",
211 PRINT C$(I), FOR I = 1 TO N1 + N2
212 PRINT "B"
220 FOR I = 1 TO N2
221 PRINT R(I), C1$(I),
222 PRINT X(I, J), FOR J = 1 TO N1 + N2
230 PRINT B(I)
239 NEXT I
300 REM EVALUATE TABLEAU FOR OPTIMUM SOLUTION
301 DIM Z(N1 + N2)
302 Z(J) = 0 FOR J = 1 TO N1 + N2
305 FOR I = 1 TO N2
306 Z(J) = Z(J) + R(I) * X(I, J) FOR J = 1 TO N1 + N2
307 NEXT I
310 REM COMPUTE Z - C
311 ARR Y = Z - C
320 REM CHECK FOR OPTIMUM, IDENTIFY KEY COLUMN
321 J0 = 0
322 M = 0
323 FOR J = 1 TO N1 + N2
324 IF Y(J) >= M THEN 327
325 J0 = J
326 M = Y(J)
327 NEXT J
328 IF M >= 0 THEN 500
329 PRINT "NON-OPTIMUM."
330 REM EVALUATE BASIC VARIABLES
331 DIM W(N2)
332 W(I) = 0 FOR I = 1 TO N2
333 W(I) = B(I) / X(I, J0) IF X(I, J0) <> 0 FOR I = 1 TO N2
340 REM PRINT TABLEAU WITH EVALUATION ROWS AND COLUMNS
341 PRINT
342 PRINT "EXPANDED TABLEAU"
343 PRINT
344 PRINT " ", "C->",
345 ARR PRINT C,
350 PRINT "CB", "BV",
351 PRINT C$(I), FOR I = 1 TO N1 + N2
352 PRINT "B", "B / "; C$(J0)
360 FOR I = 1 TO N2
361 PRINT R(I), C1$(I),
362 PRINT X(I, J), FOR J = 1 TO N1 + N2
363 PRINT B(I), W(I)
364 NEXT I
370 PRINT " ", "Z",
371 ARR PRINT Z
372 PRINT " ", "Z-C",
373 ARR PRINT Y
374 PRINT "KEY COLUMN"; J0; "VARIABLE "; C$(J0); " ENTERS"
380 REM IDENTIFY KEY ROW (SMALLEST POSITIVE VALUE OF W)
381 I0 = 0
382 M = 1E6
383 FOR I = 1 TO N2
384 IF W(I) <= 0 OR W(I) >= M THEN 387
385 I0 = I
386 M = W(I)
387 NEXT I
389 PRINT "KEY ROW"; I0; "VARIABLE "; C1$(I0); " EXITS"
400 REM PIVOT ON KEY CELL
401 REM CHANGE BASIC VARIABLE NAME AND COEFFICIENT
402 C1$(I0) = C$(J0)
403 R(I0) = C(J0)
410 REM BUILD NEW MATRIX X2 AND ARRAY B2
411 MAT X2 = X
412 ARR B2 = B
420 REM ADJUST VALUES IN KEY ROW
421 FOR J = 1 TO N1 + N2
422 X2(I0, J) = X(I0, J) / X(I0, J0)
423 NEXT J
430 REM ADJUST VALUES IN OTHER ROWS
431 FOR I = 1 TO N2
432 IF I = I0 THEN 436
433 FOR J = 1 TO N1 + N2
434 X2(I, J) = X(I, J) - X(I, J0) * X(I0, J) / X(I0, J0)
435 NEXT J
436 NEXT I
440 REM ADJUST VALUES IN RIGHT-HAND-SIDE
441 B2(I0) = B(I0) / X(I0, J0)
442 FOR I = 1 TO N2
443 IF I = I0 THEN 445
444 B2(I) = B(I) - X(I, J0) * B(I0) / X(I0, J0)
445 NEXT I
450 REM SWAP IN THE NEW MATRIX AND CONSTANTS
451 MAT X = X2
452 ARR B = B2
453 GOTO 200
500 REM NO MORE CHANGES
501 PRINT "SOLUTION FOUND"
511 PRINT " ", "Z",
512 ARR PRINT Z
513 PRINT " ", "Z-C",
514 ARR PRINT Y
520 PRINT "OBJECTIVE VALUE"
521 Z = 0
522 FOR I = 1 TO N2
523 PRINT C1$(I); " = "; B(I); " * "; R(I)
524 Z = Z + B(I) * R(I)
525 NEXT I
527 PRINT "Z = "; Z
999 STOP
4000 REM NUMBER DECISION VARS, NUMBER CONSTRAINTS
4001 DATA 2, 4
4010 REM COEFFICIENTS FOR OBJECTIVE FUNCTION (WITHOUT SLACK)
4011 DATA 20, 14
4020 REM CONSTRAINT COEFFICIENTS (WITHOUT SLACK)
4021 DATA 6, 2, 8, 6, 6, 4, 40, 25
4030 REM RIGHT-HAND SIDE CONSTANTS
4031 DATA 2000, 2000, 1440, 9600
4999 END
